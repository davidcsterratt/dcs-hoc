if (name_declared("pkgversions") != 4 ) {  execute("strdef pkgversions") }
sprint(pkgversions,"%sTreePlot = $Revision: 1.3 $, ",pkgversions)

load_file("TreePlot.hoc")
load_file("RPlot.hoc")

begintemplate TreePlotArray
public glist, cellref, rprintfile

objref vb
objref hblist
objref glist
objref rp
strdef rfilename

// TreePlotArray(double rows, double cols)
proc init() { local i, j
    rows = $1
    cols = $2
    vb = new VBox()
    hblist = new List()
    glist = new List()
    vb.intercept(1)
    for i=0,rows-1 {
        hblist.append(new HBox())
        hblist.object(i).intercept(1)
        for j = 0,cols-1 {
            glist.append(new TreePlot())
        }
        hblist.object(i).intercept(0)
        hblist.object(i).map
    }
    vb.intercept(0)
    vb.map
    rfilename = "/tmp/TreePlotArray"
}

proc cellref() {
    $o1 = glist.object($2*cols + $3)
}

proc rprintfile() { local i, j
    rp = new RPlot(rfilename)
    rp.start_plot($s1)
    rp.set_mfrow(rows,cols)
    for i=0,rows-1 {
        for j = 0,cols-1 {
            rp.add_graph(glist.object(i*cols+j))
        }
    }
    rp.end_plot()
    rp.run()
}

endtemplate TreePlotArray